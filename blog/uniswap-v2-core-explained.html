<h1>Uniswap V2原理详解</h1>
<address>by Moomoo</address>

<h2>导言</h2>
<p>Uniswap是以太坊上规模最大的去中心化交易所，其采用的<strong>自动做市商算法（Automated Market Maker，AMM）</strong>算法颠覆了传统交易所的订单簿的交易形式。Uniswap V2是被广泛使用的一个版本，其内部的AMM算法以乘法模型为基础，通过流动性供应商（Liquidity Provider，LP)提供流动性来支持币对的交易，并将手续费分发给提供流动性的供应商，采用这种激励方式来鼓励供应商提供流动性，支持其运行。本文将详细介绍Uniswap V2的内部原理。</p>
<h2>订单薄和流动性池</h2>
<p>在传统的交易所中，交易者通过标价出售或投标买入来进行交易，也就是提交卖单或是买单。交易所维护一份订单簿，记录当前所有的买单和买单，并使用算法来匹配合适的买卖单，促成交易。买单和卖单的提交，本质上是交易者希望用一种币去换另一种币。比如在ETH/USDT交易对中，交易者提交买单，本质是希望使用USDT去换ETH，而价格则是使用USDT兑换ETH的比例，当碰到有交易者提交更低价格的卖单时，交易完成，买卖双方完成USDT和ETH的交换。</p>
<p>在传统的订单簿交易模型中，交易者与<strong>交易对手（counterparty）</strong>进行币种的交换，而在AMM算法中，对于每个交易对，合约维护了一个装有两个币种的池子，叫做<strong>流动性池（Liquidity Pool）</strong>，比如在ETH/USDT交易对中，就维护了一个装有ETH和USDT的池子，交易者可以往池子里放入USDT，并按照一定比例换取ETH，反之亦然。而流动性池中的币则由流动性供应商提供，算法则会从每一次交易中抽取手续费给流动性供应商，以此来激励供应商提供流动性。与订单簿不同的是，订单簿中交易者可以限定价格等待成交，而在AMM中则需要按照当前池子计算出的价格进行交易。</p>
<p>接下来，我们讨论一下AMM算法的原理。</p>

<h2>固定乘积的做市商模型</h2>
<p>Uniswap V2交易使用的是固定乘积的做市商模型（Constant Product Market Maker Model）。在一个币对的流动性池中，每次交易后，两个币种数量的乘积不变，保持为常数\(k\)（在实际的交易中，因为存在手续费和整型精度问题的影响，交易后\(k\)的值会有所改变，但算法保证 \(k\)值不会减小）。</p>
<h3>将固定数量的币换成另一种币</h3>
<p>假设一个池子由A和B两种币组成（ETH、USDT、AAVE等），当前池子中A币的数量为\(x\)，B币的数量为\(y\)，两币数量乘积为\(k\)，即：\(x * y = k\)。在交易时，假设交易者使用数量为\(\Delta x\)的A币换到了数量为\(\Delta y\)的B币，则交换前后，池子中两种币的数量满足以下等式（暂时不考虑手续费）：</p>
<p>$$ x * y = (x + \Delta x) \cdot (y - \Delta y) = k \tag{1} $$</p>
<p>对上述等式进行变换，可以得到下面的式子：</p>
<p>$$ \Delta y = \frac{\Delta x}{x + \Delta x} \cdot y \tag{2}$$</p>
<p>在Uniswap V2中，每次交易会抽取0.3%的手续费，记为\(\rho\)，即 \(\rho = 0.003\)。这笔手续费从 \(\Delta x\)中抽取，故只有\((1 - \rho) \cdot \Delta x\)的A币会用于交换成B币，剩下\(\rho \cdot \Delta x\)的A币会作为手续费添加到池子中。此时，上面的等式就会变成：</p>
<p>$$ x * y = (x + (1 - \rho) \cdot \Delta x) \cdot (y - \Delta y) = k \tag{3} $$</p>
<p>在这个等式下，\(\Delta y\)的计算方式就变成了：</p>
<p>$$ \Delta y = \frac{(1 - \rho) \cdot \Delta x}{x + (1 - \rho) \cdot \Delta x} \cdot y \tag{4} $$</p>
<p>将\((1 - \rho)\)记作\(\gamma\)，即\(\gamma = 1 - \rho = 0.997\)，公式可改写成：</p>
<p>$$ \Delta y = \frac{\gamma \cdot \Delta x}{x + \gamma \cdot \Delta x} \cdot y \tag{5} $$</p>
<p>这就是Uniswap V2在进行交易时所采用的计算方法。可以看出，在引入手续费后，同样数量的A币能换到的B币，即\(\Delta y\)就变少了，而加到池子中的A币数量\(\Delta x\)没有变化，所以\((x + \Delta x) \cdot (y - \Delta y)\)就会稍大于\(x \cdot y\)，也就是说\(k\)值会稍微增长。故乘积并不是严格的常数。</p>
<p>在Uniswap V2中，计算交换得到的代币数量的代码如下：</p>
<pre><code
    class="language-sol"
>// @uniswap/v2-periphery/contracts/libraries/UniswapV2Library.sol
function getAmountOut(uint amountIn, uint reserveIn, uint reserveOut) internal pure returns (uint amountOut) {
    // reserveIn和reserveOut分别是当前池子中两种币的数量
    require(amountIn > 0, 'UniswapV2Library: INSUFFICIENT_INPUT_AMOUNT');
    require(reserveIn > 0 && reserveOut > 0, 'UniswapV2Library: INSUFFICIENT_LIQUIDITY');
    // 用于交换的币需要扣除手续费
    uint amountInWithFee = amountIn.mul(997);
    uint numerator = amountInWithFee.mul(reserveOut);
    // 这里将分子分母都放大了1000倍，避免小数
    uint denominator = reserveIn.mul(1000).add(amountInWithFee);
    // 这里向下取整
    amountOut = numerator / denominator;
}
</code></pre>
<h3>将一种币换成固定数量的另一种币</h3>
<p>假设在A/B交易对中，交易者希望换取\(\Delta y\)数量的B币，那么他需要花费多少A币呢？根据等式\((3)\)，我们可以推算出下面的等式：</p>
<p>$$ \Delta x = \frac{x \cdot \Delta y}{\gamma \cdot (y - \Delta y)} \tag{6} $$</p>
<p>在Uniswap V2中，\(\gamma = 1 - \rho = 1 - 0.003 = 0.997\)。在合约中，代码如下：</p>
<pre><code
    class="language-sol"
>// @uniswap/v2-periphery/contracts/libraries/UniswapV2Library.sol
function getAmountIn(uint amountOut, uint reserveIn, uint reserveOut) internal pure returns (uint amountIn) {
    require(amountOut > 0, 'UniswapV2Library: INSUFFICIENT_OUTPUT_AMOUNT');
    require(reserveIn > 0 && reserveOut > 0, 'UniswapV2Library: INSUFFICIENT_LIQUIDITY');
    // 这里将分子分母都放大了1000倍，避免小数
    uint numerator = reserveIn.mul(amountOut).mul(1000);
    uint denominator = reserveOut.sub(amountOut).mul(997);
    // 这里向上取整
    amountIn = (numerator / denominator).add(1);
}
</code></pre>
<p><em>PS：在计算获得的代币时，合约向下取整，而在计算需要的代币时，合约是向上取整，这里避免因为整数精度问题造成的\(k\)值减小。（黑心）</em></p>
<h3>兑换的比例</h3>
<p>我们观察公式\((5)\)和\((6)\)，对其进行一些变换，就能得到：</p>
<p>$$ \frac{\Delta x}{\Delta y} = \frac{x}{\gamma \cdot (y - \Delta y)} = \frac{x + \gamma \cdot \Delta x}{\gamma \cdot y} \tag{7} $$</p>
<p>当\(\Delta x\)相较于\(x\)比较小时，\(\Delta x\)与\(\Delta y\)的比例约等于\(x\)与\(y\)的比例，所以在数额较小（或池子较大）的交易中，可以通过池子中两种币的比例来估算出能换到多少币。也就是说池子中两种币的比例，可以大致反映币的价格。</p>
<p>例如，在ETH/USDT交易对中，池子里有5个ETH和15000个USDT，那么就能用\(15000 / 5 = 3000\)来反映ETH当前的价格。</p>

<h3>关于滑点</h3>
<p>在Uniswap V2的合约中，并没有对于滑点的计算，Uniswap V2的前端利用页面滑点值计算出其他的参数。在<code>swapExactTokensForTokens</code>、<code>swapExactETHForTokens</code>和<code>swapExactTokensForETH</code>方法中，前端根据滑点计算出交换得到币的最少数量<code>amountOutMin</code>并作为参数传入，要求交易得到的币不低于该最少数量，不然交易失败。</p>
<p>在<code>swapTokensForExactTokens</code>和<code>swapTokensForExactETH</code>方法中，前端根据滑点计算出交易所能提供币的最大数量<code>amountInMax</code>，合约保证交易所需的币不超过该数量，不然交易失败。在<code>swapETHFoExactTokens</code>方法中，没有显式的参数传入，而用交易者发送给合约的ETH数量<code>msg.value</code>作为输入上限。</p>
<h3>交易路由</h3>
<p>并不是任意两种币都有可互换的池子，假如用户想要把USDT换成SHIB，但没有SHIB/USDT的池子（只是举个例子，Uniswap中应该有这个池子），而只有ETH/USDT和SHIB/ETH的池子，那么，交易员就需要先将USDT换成ETH，再用ETH去兑换SHIB。Uniswap V2的合约支持多池子的连锁交易，在交易时，在参数path中提供兑换链路上各个币的合约，该例子中顺序就是<em>（USDT->ETH->SHIB）</em>，智能合约就会按照该顺序，首先去ETH/USDT池子中将USDT换成ETH，再去SHIB/ETH池子中将ETH换成SHIB。在每个池子中兑换都需要缴纳手续费，所以交易应该避免交易的路由过长。 </p>
<p>交易的路由，即path参数一般由Uniswap的api进行计算，无需手动计算。如果想进行特殊的交易，也可以手动输入path参数进行交易。</p>
<h2>流动性池</h2>
<p>对于两种币A和B，如果需要在Uniswap上交易，则首先需要通过调用<code>UniswapV2Factory.sol（工厂）</code>合约的<code>createPair</code>方法创建交易对合约<code>UniswapV2Pair.sol</code>的实例并部署在链上。</p>
<p><code>UniswapV2Pair</code>合约用于存储A和B两种代币，来组成流动性池，而该合约本身又实现了ERC-20的接口，所以其本身也是一种代币合约。每种币对都对应唯一的代币合约，该代币被称为<strong>流动性代币（Liquidity Token）</strong>，作为提供流动性的凭证。在流动性供应商提供流动性时，合约铸造流动性代币给流动性供应商，在抽取流动性时，合约销毁代币，并给流动性供应商返还对应的流动性份额和手续费。</p>
<h3>增加流动性</h3>
<p>增加流动性，也就是向池子中增加币对，在A/B交易对中，流动性提供商可以向池子中提供A币和B币，来赚取交易的手续费。那么，提供A、B两种币的数量怎么计算呢？</p>
<p>假设交易对A/B刚被创建，池子中还没有A币或是B币，也就是没有流动性，那么第一个提供流动性的人，可以提供任意数量的两种币。假设供应商提供了\(x\)个A币和\(y\)个B币，那么合约将会铸造\(\sqrt{x \cdot y}\)个流动性代币。此处为了避免之后出现数值计算上的错误，合约会将首次铸造的1000个单位的流动性代币销毁掉（因为代币常用的精度是18位，所以进行精度转换后是\(10^{-15}\)个流动性代币，可以忽略不计）。最终，首次提供流动性的供应商能拿到\(\sqrt{x \cdot y} - 10^{-15}\)个流动性代币。</p>
<p>如果池子中已经有币了，那么流动性供应商只能按照池子中当前币的数量来提供流动性。假设当前池子中有\(x\)个A币和\(y\)个B币，且总的流动性代币（包括被销毁掉的\(10^{-15}\)个）数量为\(l\)。那么，当流动性供应商想提供\(\Delta x\)的A币和\(\Delta y\)的B币，则必须满足：</p>
<p>$$ \frac{\Delta x}{\Delta y} = \frac{x}{y} \tag{8} $$</p>
<p>当供应商按要求的比例提供A币和B币后，他将会得到\(\Delta l\)个流动性代币，其中\(\Delta l\)的计算方法如下：</p>
<p>$$ \Delta l = \frac{\Delta x}{x} \cdot l = \frac{\Delta y}{y} \cdot l \tag{9} $$</p>
<p>在Uniswap V2中，增加流动性的代码如下：</p>
<pre><code
    class="language-sol"
>// @uniswap/v2-core/contracts/UniswapV2Pair.sol
function mint(address to) external lock returns (uint liquidity) {
    (uint112 _reserve0, uint112 _reserve1,) = getReserves(); // gas savings
    uint balance0 = IERC20(token0).balanceOf(address(this));
    uint balance1 = IERC20(token1).balanceOf(address(this));
    // 此处是两种币的增加数量
    uint amount0 = balance0.sub(_reserve0);
    uint amount1 = balance1.sub(_reserve1);

    // 此处是协议手续费，目前为0，暂不考虑
    bool feeOn = _mintFee(_reserve0, _reserve1);
    uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
    if (_totalSupply == 0) {
        liquidity = Math.sqrt(amount0.mul(amount1)).sub(MINIMUM_LIQUIDITY);
       _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
    } else {
        // 整型除法，选最小值（黑心）
        liquidity = Math.min(amount0.mul(_totalSupply) / _reserve0, amount1.mul(_totalSupply) / _reserve1);
    }
    require(liquidity > 0, 'UniswapV2: INSUFFICIENT_LIQUIDITY_MINTED');
    _mint(to, liquidity);

    _update(balance0, balance1, _reserve0, _reserve1);
    if (feeOn) kLast = uint(reserve0).mul(reserve1); // reserve0 and reserve1 are up-to-date
    emit Mint(msg.sender, amount0, amount1);
}
</code></pre>
<h3>移除流动性</h3>
<p>移除流动性时，供应商向流动性池发送流动性代币，并赎回对应份额的代币和手续费。与增加流动性相同，移除流动性时也是按照比例移除。假设当前A/B交易对的池子中有\(x\)个A币和\(y\)个B币，流动性代币数量为\(l\)，供应商准备移除\(\Delta l\)的流动性，那么，他将会收到\(\Delta x\)的A币和\(\Delta y\)的B币，计算方法如下：</p>
<p>$$ \Delta x = \frac{\Delta l}{l} \cdot x \tag{10} $$</p>
<p>$$ \Delta y = \frac{\Delta l}{l} \cdot y \tag{11} $$</p>
<p>因为每次交易的手续费都会被扣除并添加到池子中，故\(\Delta x\)和\(\Delta y\)中已包括了对应份额的手续费收益。</p>
<p>Uniswap V2中移除流动性的代码如下：</p>
<pre><code
    class="language-sol"
>// @uniswap/v2-core/contracts/UniswapV2Pair.sol
function burn(address to) external lock returns (uint amount0, uint amount1) {
    (uint112 _reserve0, uint112 _reserve1,) = getReserves(); // gas savings
    address _token0 = token0;                                // gas savings
    address _token1 = token1;                                // gas savings
    uint balance0 = IERC20(_token0).balanceOf(address(this));
    uint balance1 = IERC20(_token1).balanceOf(address(this));
    // 此处是发送到池子的流动性代币
    uint liquidity = balanceOf[address(this)];

    // 协议手续费，当前为0，暂不考虑
    bool feeOn = _mintFee(_reserve0, _reserve1);
    uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
    // 计算能领取的代币数量
    amount0 = liquidity.mul(balance0) / _totalSupply; // using balances ensures pro-rata distribution
    amount1 = liquidity.mul(balance1) / _totalSupply; // using balances ensures pro-rata distribution
    require(amount0 > 0 && amount1 > 0, 'UniswapV2: INSUFFICIENT_LIQUIDITY_BURNED');
    _burn(address(this), liquidity);
    _safeTransfer(_token0, to, amount0);
    _safeTransfer(_token1, to, amount1);
    balance0 = IERC20(_token0).balanceOf(address(this));
    balance1 = IERC20(_token1).balanceOf(address(this));

    _update(balance0, balance1, _reserve0, _reserve1);
    if (feeOn) kLast = uint(reserve0).mul(reserve1); // reserve0 and reserve1 are up-to-date
    emit Burn(msg.sender, amount0, amount1, to);
}
</code></pre>

<h3>协议手续费</h3>
<p>Uniswap V2保留了一个协议手续费的开关，如果打开，则Uniswap协议会从每次交易中收取0.05%的手续费，这笔手续费会从流动性供应商赚取的手续费中扣除。也就是说，一笔交易收取0.3%的手续费中，有0.25%会给流动性供应商，而剩下的0.05%会给到协议所设置的<code>feeTo</code>地址。当前该暂时不收取该手续费，但以后有可能会开启（开启后Uniswap简直就是印钞机）。</p>

<h2>无常损失</h2>
<p><strong>无常损失（Impermanent Loss）</strong>是一种流动性供应商的暂时/非永久损失。当币价出现较大波动时，流动性供应商所持有的流动性份额所能兑换到的币的价值低于提供流动性时发送给池子的币的价值就叫做无常损失。</p>
<p>举个例子，假设当前在ETH/USDT的池子中有1个ETH和3000个USDT，ETH价格为3000U，此时用户A提供了1个ETH和3000个USDT，两者的总价值为6000U（1ETH = 3000U + 3000USDT），并获得1/2的流动性份额。此时池子里有2个ETH和6000个USDT。</p>
<p>如果此时ETH价格暴涨到12000U。那么就会有套利者来Uniswap进行套利，最后池子里的会剩下1个ETH和12000U，正好反应当前ETH的价格。如果此时用户A提取出他的1/2份额，他能得到0.5个ETH和6000个USDT，价值12000U（0.5ETH = 6000U + 12000U）。</p>
<p>这么看来，似乎流动性供应商大赚6000U，但是如果该流动商不提供流动性，从一开始就拿着1个ETH和3000个USDT，那么现在总价值就是15000U（1ETH = 12000U + 3000USDT）。在该过程中，流动性供应商损失了3000U，这部分就是AMM算法造成的无常损失。</p>
<p>无常损失是非永久的，如果币价回落到原先水平，那么无常损失就会消失，而且流动性供应商能够赚取手续费，对于价格比较稳定的币对，手续费收益能远超无常损失，所以流动性供应商是有动力去提供流动性的。不过无常损失也会带来风险，所以提供流动性时要计算好风险收益比。</p>

<h2>参考资料</h2>
<ol>
    <li><a href="https://uniswap.org/whitepaper.pdf"><p>Uniswap V2 Core</p></a></li>
    <li><a href="https://github.com/runtimeverification/verified-smart-contracts/blob/uniswap/uniswap/x-y-k.pdf"><p>Formal Specification of Constant Product (x × y = k) Market Maker Model and Implementation</p></a></li>
    <li><a href="https://academy.binance.com/zh/articles/impermanent-loss-explained"><p>什么是无常损失|币安学院</p></a></li>
</ol>
